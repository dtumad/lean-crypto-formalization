import analysis.asymptotics.asymptotics
import analysis.special_functions.polynomials
import analysis.special_functions.exp_log
import to_mathlib

/-!
# Polynomial Growth

This file defines polynomial growth of functions via `poly_growth_in_parameter`.
This definition is given in terms of a parameter function `k : Î± â†’ R`,
 which can specialize to e.g. `id` or `log` to represent polynomial and polylogarithmic growth resp.

The main defintion is given in terms of powers, see `poly_growth_in_parameter_iff` for an equivalent
 formulation in terms of `polynomial`.
-/

section poly_growth_in_parameter

open polynomial asymptotics

/-- A function `f` has polynomial growth in the parameter `k` if `f(x) = O(k(x)^n)` for some `n : â„•`
  This is equivalent to `f(x) = O(p âˆ˜ k)` for some polynomial `p`, see `poly_growth_in_parameter_iff` -/
def poly_growth_in_parameter {Î± R S : Type*} [preorder Î±] [normed_ring R] [normed_ring S]
  (k : Î± â†’ R) (f : Î± â†’ S) :=
âˆƒ (n : â„•), is_O f (Î» x, (k x) ^ n) filter.at_top

variables {Î± : Type*} [preorder Î±]
variables {R S : Type*} [normed_ring R] [normed_ring S]
variables {ğ•œ : Type*} [normed_field ğ•œ]

lemma poly_growth_in_parameter_def (k : Î± â†’ R) (f : Î± â†’ S) :
  poly_growth_in_parameter k f â†” âˆƒ (n : â„•), is_O f (Î» x, (k x) ^ n) filter.at_top :=
iff.rfl

lemma poly_growth_in_parameter.ext {k : Î± â†’ R} {f g : Î± â†’ S}
  (hf : poly_growth_in_parameter k f) (hfg : âˆ€ x, g x = f x) :
  poly_growth_in_parameter k g :=
(funext hfg : g = f).symm â–¸ hf

@[simp] lemma poly_growth_in_parameter_parameter (k : Î± â†’ R) :
  poly_growth_in_parameter k k :=
âŸ¨1, by simpa using is_O_refl k filter.at_topâŸ©

variable [norm_one_class R]

@[simp] 
lemma poly_growth_in_parameter_const (k : Î± â†’ R) (s : S) :
  poly_growth_in_parameter k (Î» _, s) :=
âŸ¨0, is_O_of_le' filter.at_top (Î» x, by simp : âˆ€ x, âˆ¥sâˆ¥ â‰¤ âˆ¥sâˆ¥ * âˆ¥k x ^ 0âˆ¥)âŸ©

lemma poly_growth_in_parameter_one (k : Î± â†’ R) :
  poly_growth_in_parameter k (1 : Î± â†’ S) :=
poly_growth_in_parameter_const k 1

lemma poly_growth_in_parameter_zero (k : Î± â†’ R) :
  poly_growth_in_parameter k (0 : Î± â†’ S) :=
poly_growth_in_parameter_const k 0

/-- If the parameter is eventually greater than `1`, then polynomial growth in `k` is additive -/
lemma poly_growth_in_parameter_add {k : Î± â†’ ğ•œ}
  {f g : Î± â†’ R} (hk : âˆ€á¶  x in filter.at_top, 1 â‰¤ âˆ¥k xâˆ¥)
  (hf : poly_growth_in_parameter k f) (hg : poly_growth_in_parameter k g) :
  poly_growth_in_parameter k (f + g) :=
let âŸ¨n, hnâŸ© := hf in let âŸ¨m, hmâŸ© := hg in
âŸ¨max n m, is_O.add (hn.trans $ is_O_of_pow_le hk (le_max_left n m)) 
    (hm.trans $ is_O_of_pow_le hk (le_max_right n m))âŸ©

/-- Polynomial growth is multiplicative regardless of the parameter-/
lemma poly_growth_in_parameter_mul {k : Î± â†’ ğ•œ} {f g : Î± â†’ R}
  (hf : poly_growth_in_parameter k f) (hg : poly_growth_in_parameter k g) :
  poly_growth_in_parameter k (f * g) :=
let âŸ¨n, hnâŸ© := hf in let âŸ¨m, hmâŸ© := hg in
âŸ¨n + m, (is_O.mul hn hm).trans $ is_O_of_le filter.at_top (Î» x, (pow_add (k x) n m) â–¸ le_rfl)âŸ©

lemma poly_growth_in_parameter_pow {k : Î± â†’ ğ•œ} {f : Î± â†’ R}
  (hf : poly_growth_in_parameter k f) (n : â„•) :
  poly_growth_in_parameter k (Î» x, f x ^ n) :=
let âŸ¨m, hmâŸ© := hf in
âŸ¨m * n, (is_O.pow hm n).trans $ is_O_of_le filter.at_top (Î» x, (pow_mul (k x) m n) â–¸ le_rfl)âŸ©

lemma poly_growth_in_parameter_polynomial {k : Î± â†’ ğ•œ} (hk : âˆ€á¶  x in filter.at_top, 1 â‰¤ âˆ¥k xâˆ¥) 
  {f : Î± â†’ R} (hf : poly_growth_in_parameter k f)
  (p : polynomial R) : poly_growth_in_parameter k (Î» x, eval (f x) p) :=
begin
  refine p.induction_on (Î» c, _) (Î» p q hp hq, _) (Î» n c h, _),
  { exact (poly_growth_in_parameter_const k c).ext (Î» x, eval_C) },
  { exact (poly_growth_in_parameter_add hk hp hq).ext (Î» x, eval_add) },
  { exact (poly_growth_in_parameter_mul h (hf)).ext
      (Î» x, by simp only [eval_C, eval_mul_X_pow, pi.mul_apply, pow_add (f x) n 1, mul_assoc, pow_one]) }
end

/-- Equivalence of definition in terms of powers and polynomials,
  assuming `ğ•œ` is a `normed_linear_ordered_field` with an ordered topology (e.g. `â„` or `â„š`) -/
theorem poly_growth_in_parameter_iff {ğ•œ : Type*} [normed_linear_ordered_field ğ•œ] [order_topology ğ•œ] 
  {k : Î± â†’ ğ•œ} (hk : filter.tendsto k filter.at_top filter.at_top) (f : Î± â†’ R) :
  poly_growth_in_parameter k f â†” 
    âˆƒ (p : polynomial ğ•œ), is_O f (Î» x, eval (k x) p) filter.at_top :=
begin
  refine âŸ¨Î» h, let âŸ¨n, hnâŸ© := h in âŸ¨X ^ n, by simpaâŸ©, _âŸ©,
  rintro âŸ¨p, hpâŸ©,
  refine âŸ¨p.nat_degree, is_O.trans hp _âŸ©,
  suffices : is_O ((Î» a, eval a p) âˆ˜ k) ((Î» a, eval a (X ^ p.nat_degree)) âˆ˜ k) filter.at_top,
  by simpa using this,
  have := polynomial.is_O_of_degree_le p (X ^ p.nat_degree) (by simp),
  refine is_O.comp_tendsto this hk,
end

end poly_growth_in_parameter

section poly_growth

/-- A function `f : â„• â†’ R` has polynomial growth if it is O(p(n)) for some `p : polynomial R`-/
def poly_growth {R : Type*} [preorder R] [normed_ring R] (f : â„• â†’ R) :=
poly_growth_in_parameter (Î» n, â†‘n : â„• â†’ â„š) f

variables {R : Type*} [preorder R] [normed_ring R] 

@[simp] lemma poly_growth_const (r : R) : 
  poly_growth (Î» _, r) :=
poly_growth_in_parameter_const _ _

@[simp] lemma poly_growth_zero :
  poly_growth (0 : â„• â†’ R) :=
poly_growth_in_parameter_zero _

@[simp] lemma poly_growth_one :
  poly_growth (1 : â„• â†’ R) :=
poly_growth_in_parameter_one _

-- TODO: generalize `poly_growth` to any situation allowing *something* like this
lemma h_help : âˆ€á¶  (x : â„•) in filter.at_top, 1 â‰¤ âˆ¥(x : â„š)âˆ¥ :=
begin
  rw filter.eventually_at_top,
  refine âŸ¨1, Î» x hx, le_abs.2 $ or.inl _âŸ©,
  simpa only [nat.one_le_cast, rat.cast_coe_nat],
end

variables [norm_one_class R]

lemma poly_growth_add {f g : â„• â†’ R} (hf : poly_growth f) (hg : poly_growth g) :
  poly_growth (f + g) :=
poly_growth_in_parameter_add h_help hf hg

end poly_growth

section log_poly_growth

def log_poly_growth {R : Type*} [normed_ring R] (f : â„ â†’ R) :=
poly_growth_in_parameter (real.log) f

lemma log_poly_growth_add {f g : â„ â†’ â„}
  (hf : log_poly_growth f) (hg : log_poly_growth g) :
  log_poly_growth (f + g) :=
poly_growth_in_parameter_add one_eventually_le_log hf hg

end log_poly_growth