import analysis.asymptotics.asymptotics
import data.polynomial
import data.real.nnreal
import analysis.special_functions.polynomials
import analysis.special_functions.exp_log

universes u v

open_locale nnreal

open asymptotics

section to_mathlib
-- General facts that should probably be ported to mathlib eventually

lemma is_O_at_top_iff {Œ± Œ≤ Œ≥ : Type*} [nonempty Œ±]
  [semilattice_sup Œ±] [semilattice_sup Œ≤] [semilattice_sup Œ≥]
  [has_norm Œ≤] [has_norm Œ≥] (f : Œ± ‚Üí Œ≤) (g : Œ± ‚Üí Œ≥) :
  is_O f g filter.at_top ‚Üî ‚àÉ M x‚ÇÄ, ‚àÄ x, x‚ÇÄ ‚â§ x ‚Üí ‚à•f x‚à• ‚â§ M * ‚à•g x‚à• :=
by simp only [is_O_iff, filter.eventually_at_top]

lemma norm_le_norm_add_const_of_dist_le {Œ± : Type u} [normed_group Œ±]
  {a b : Œ±} {c : ‚Ñù} (h : dist a b ‚â§ c) : ‚à•a‚à• ‚â§ ‚à•b‚à• + c :=
calc ‚à•a‚à• = ‚à•a - b + b‚à• : by rw sub_add_cancel a b
    ... ‚â§ ‚à•a - b‚à• + ‚à•b‚à• : norm_add_le (a - b) b
    ... = (dist a b) + ‚à•b‚à• : by rw normed_group.dist_eq
    ... ‚â§ c + ‚à•b‚à• : add_le_add h le_rfl
    ... ‚â§ ‚à•b‚à• + c : by rw (add_comm c ‚à•b‚à•)

theorem is_O_at_top_of_div_tends_to_finite 
  {ùïú Œ± : Type*} [linear_order Œ±] [nonempty Œ±] [normed_field ùïú]
  {f g : Œ± ‚Üí ùïú} (hgf : ‚àÄ·∂† x in filter.at_top, g x = 0 ‚Üí f x = 0)
  (c : ùïú) (h : filter.tendsto (f / g) filter.at_top (nhds c)) :
  is_O f g filter.at_top :=
begin
    simp only [is_O_iff, filter.eventually_at_top],
  
  use (‚à•c‚à• + 1),
  rw filter.tendsto_iff_eventually at h,
  let h' := @h (Œª (x : ùïú), ‚à•x‚à• ‚â§ ‚à•c‚à• + 1) begin
    rw filter.eventually_iff_exists_mem,
    refine ‚ü®metric.ball c 1, metric.ball_mem_nhds c zero_lt_one, Œª y hy, _‚ü©,
    exact norm_le_norm_add_const_of_dist_le (le_of_lt hy),
  end,
  rw filter.eventually_at_top at h' hgf,
  obtain ‚ü®x‚ÇÄ, h‚ü© := hgf,
  obtain ‚ü®x‚ÇÄ', h'‚ü© := h',
  refine ‚ü®max x‚ÇÄ x‚ÇÄ', Œª x hx, _‚ü©,
  rw ge_iff_le at hx,
  rw max_le_iff at hx,
  specialize h x hx.1,
  specialize h' x hx.2,
  simp only [pi.div_apply, normed_field.norm_div] at h',
  by_cases hfx : f x = 0,
  { have : ‚à•f x‚à• = 0 := trans (congr_arg _ hfx) norm_zero,
    refine this.symm ‚ñ∏ mul_nonneg (by simpa [hfx] using h') (norm_nonneg (g x)) },
  { replace h := (mt h) hfx,
    rwa div_le_iff _ at h',
    refine lt_of_le_of_ne (norm_nonneg (g x)) (Œª h', h _),
    refine norm_eq_zero.mp h'.symm }
end

lemma polynomial_exists_max_root {Œ± : Type*} [integral_domain Œ±]
  [linear_order Œ±]
  (p : polynomial Œ±) (hp : p ‚â† 0) :
  ‚àÉ x‚ÇÄ, ‚àÄ x, p.is_root x ‚Üí x ‚â§ x‚ÇÄ :=
begin
  by_cases h : list.maximum (multiset.to_list p.roots) = none,
  { rw list.maximum_eq_none at h,
    refine ‚ü®0, Œª a _, _‚ü©,
    have : a ‚àà (multiset.to_list p.roots),
    by rwa [multiset.mem_to_list, polynomial.mem_roots hp],
    rw h at this,
    refine absurd this (list.not_mem_nil a) },
  { rw [‚Üê ne.def, option.ne_none_iff_exists] at h,
    obtain ‚ü®x‚ÇÄ, hx‚ÇÄ‚ü© := h,
    refine ‚ü®x‚ÇÄ, Œª x hx, list.le_maximum_of_mem _ hx‚ÇÄ.symm‚ü©,
    rwa [multiset.mem_to_list, polynomial.mem_roots hp] }
end

lemma polynomial.eventually_no_roots {ùïú : Type*} [normed_linear_ordered_field ùïú]
  (p : polynomial ùïú) (hp : p ‚â† 0) :
  ‚àÄ·∂† x in filter.at_top, ¬¨ p.is_root x :=
begin
  obtain ‚ü®x‚ÇÄ, hx‚ÇÄ‚ü© := polynomial_exists_max_root p hp,
  rw filter.eventually_at_top,
  refine ‚ü®x‚ÇÄ + 1, Œª x hx h, _‚ü©,
  refine absurd (hx‚ÇÄ x h) (not_le.mpr (lt_of_lt_of_le (lt_add_one x‚ÇÄ) hx)),
end

lemma eventually_of_imp {Œ± : Type*} {p q : Œ± ‚Üí Prop} {l : filter Œ±}
  (hpq : ‚àÄ x, p x ‚Üí q x) (h : ‚àÄ·∂† x in l, p x) : ‚àÄ·∂† x in l, q x :=
filter.mem_sets_of_superset h hpq

lemma polynomial.ne_zero_of_degree_ge_degree {R : Type*} [semiring R]
  {p q : polynomial R} (hpq : p.degree ‚â§ q.degree) (hp : p ‚â† 0) : q ‚â† 0 :=
polynomial.ne_zero_of_degree_gt (lt_of_lt_of_le (bot_lt_iff_ne_bot.mpr
  (by rwa [ne.def, polynomial.degree_eq_bot])) hpq : q.degree > ‚ä•)

theorem polynomial.is_O_of_degree_le {ùïú : Type*} [normed_linear_ordered_field ùïú] [order_topology ùïú]
  (p : polynomial ùïú) (q : polynomial ùïú) (h : p.degree ‚â§ q.degree) :
  is_O (Œª x, polynomial.eval x p) (Œª x, polynomial.eval x q) filter.at_top :=
begin
  by_cases hp : p = 0,
  { simpa [hp] using is_O_zero (Œª x, polynomial.eval x q) filter.at_top },
  { have hq : q ‚â† 0 := polynomial.ne_zero_of_degree_ge_degree h hp,
    cases le_iff_lt_or_eq.mp h with h h,
    { have := polynomial.div_tendsto_zero_of_degree_lt p q h,
      refine is_O_at_top_of_div_tends_to_finite _ 0 this,
      refine eventually_of_imp _ (q.eventually_no_roots hq),
      refine Œª x hx hx', absurd hx' hx },
    { have := polynomial.div_tendsto_leading_coeff_div_of_degree_eq p q h,
      refine is_O_at_top_of_div_tends_to_finite _ _ this,
      refine eventually_of_imp _ (q.eventually_no_roots hq),
      refine Œª x hx hx', absurd hx' hx } }
end

end to_mathlib

section poly_growth
open polynomial

/-- Predicate for functions that have polynomial growth -/
def poly_growth {R : Type u} [normed_ring R] [preorder R] (f : R ‚Üí R) :=
‚àÉ (p : polynomial R), is_O f (Œª a, eval a p) filter.at_top

variables {R : Type u} [normed_ring R] [preorder R]

@[simp] lemma poly_growth_const (b : R) : poly_growth (Œª _, b) :=
‚ü®C b, is_O_of_le filter.at_top (Œª x, by simp)‚ü©

@[simp] lemma poly_growth_one : poly_growth (1 : R ‚Üí R) :=
poly_growth_const 1

@[simp] lemma poly_growth_zero : poly_growth (0 : R ‚Üí R) :=
poly_growth_const 0

@[simp] lemma poly_growth_id : poly_growth (id : R ‚Üí R) :=
‚ü®X, is_O_of_le filter.at_top (Œª x, by simp)‚ü©

variables {ùïú : Type u} [normed_linear_ordered_field ùïú] [order_topology ùïú]

lemma poly_growth_mul {f g : ùïú ‚Üí ùïú} (hf : poly_growth f)
  (hg : poly_growth g) : poly_growth (f * g) :=
let ‚ü®p, hp‚ü© := hf, ‚ü®q, hq‚ü© := hg in 
  ‚ü®p * q, by simpa using is_O.mul hp hq‚ü©

lemma poly_growth_pow {f : ùïú ‚Üí ùïú} (hf : poly_growth f) (n : ‚Ñï) :
  poly_growth (f ^ n) :=
nat.rec_on n ((pow_zero f) ‚ñ∏ poly_growth_one)
  (Œª n hn, (pow_succ f n) ‚ñ∏ poly_growth_mul hf hn)

lemma poly_growth_iff_is_O_monomial (f : ùïú ‚Üí ùïú) :
  poly_growth f ‚Üî ‚àÉ (n : ‚Ñï), is_O f (Œª b, b ^ n) filter.at_top :=
‚ü®Œª h, let ‚ü®p, hp‚ü© := h in ‚ü®p.nat_degree, is_O.trans hp (is_O.trans
  (is_O_of_degree_le p (X ^ p.nat_degree) (by simp)) (by simp [is_O_refl]))‚ü©, 
Œª h, let ‚ü®n, hn‚ü© := h in ‚ü®X ^ n, is_O.trans hn (is_O_of_le filter.at_top (Œª x, by simp))‚ü©‚ü©

lemma poly_growth_add {f g : ùïú ‚Üí ùïú} (hf : poly_growth f)
  (hg : poly_growth g) : poly_growth (f + g) :=
let ‚ü®p, hp‚ü© := hf, ‚ü®q, hq‚ü© := hg in
begin
  by_cases hpq : p = 0 ‚à® q = 0,
  { cases hpq with hp0 hq0,
    { simp only [hp0, eval_zero] at hp,
      refine ‚ü®q, is_O.add (is_O.trans hp (is_O_zero _ _)) hq‚ü© },
    { simp only [hq0, eval_zero] at hq,
      refine ‚ü®p, is_O.add hp (is_O.trans hq (is_O_zero _ _))‚ü© } },
  { rw not_or_distrib at hpq,
    refine ‚ü®p * q, is_O.add (is_O.trans hp (is_O_of_degree_le p (p * q) (degree_le_mul_left _ hpq.2)))
      (is_O.trans hq (is_O_of_degree_le q (p * q) (mul_comm q p ‚ñ∏ (degree_le_mul_left _ hpq.1))))‚ü© }
end

end poly_growth

section log_poly_growth
open polynomial
open real

def log_poly_growth (f : ‚Ñù ‚Üí ‚Ñù) := 
‚àÉ (k : ‚Ñï), is_O f (Œª a, (log a) ^ k) filter.at_top

lemma log_poly_growth_iff (f : ‚Ñù ‚Üí ‚Ñù) :
  log_poly_growth f ‚Üî ‚àÉ (k : ‚Ñï), is_O f (Œª a, (log a) ^ k) filter.at_top :=
iff.rfl

lemma log_poly_growth_iff_is_O_log_pow (f : ‚Ñù ‚Üí ‚Ñù) :
  log_poly_growth f ‚Üî ‚àÉ (p : polynomial ‚Ñù), is_O f (Œª a, eval (log a) p) filter.at_top :=
begin
  refine ‚ü®Œª h, let ‚ü®k, hk‚ü© := h in ‚ü®X ^ k, by simpa only [eval_X, eval_pow]‚ü©, _‚ü©,
  rintro ‚ü®p, hp‚ü©,
  refine ‚ü®p.nat_degree, is_O.trans hp _‚ü©,
  suffices : is_O ((Œª a, eval a p) ‚àò log) ((Œª a, eval a (X ^ p.nat_degree)) ‚àò log) filter.at_top,
  by simpa using this,
  exact is_O.comp_tendsto (polynomial.is_O_of_degree_le _ _ (by simp)) tendsto_log_at_top,
end

@[simp] lemma log_poly_growth_const (b : ‚Ñù) : log_poly_growth (Œª _, b) :=
‚ü®0, by simpa using is_O_const_const b (zero_ne_one.symm : (1 : ‚Ñù) ‚â† 0) filter.at_top‚ü©

@[simp] lemma log_poly_growth_one : log_poly_growth 1 :=
log_poly_growth_const 1

@[simp] lemma log_poly_growth_zero : log_poly_growth 0 :=
log_poly_growth_const 0

@[simp] lemma log_poly_growth_log : log_poly_growth real.log :=
‚ü®1, by simp [is_O_refl]‚ü©

lemma log_poly_growth_mul {f g : ‚Ñù ‚Üí ‚Ñù} (hf : log_poly_growth f)
  (hg : log_poly_growth g) : log_poly_growth (f * g) :=
let ‚ü®a, ha‚ü© := hf, ‚ü®b, hb‚ü© := hg in 
  ‚ü®a + b, by simpa [pow_add] using is_O.mul ha hb‚ü©

lemma log_poly_growth_pow {f : ‚Ñù ‚Üí ‚Ñù} (hf : log_poly_growth f) (n : ‚Ñï) :
  log_poly_growth (f ^ n) :=
nat.rec_on n ((pow_zero f) ‚ñ∏ log_poly_growth_one)
  (Œª n hn, (pow_succ f n) ‚ñ∏ log_poly_growth_mul hf hn)

-- theorem poly_growth_of_log_poly_growth {f : ‚Ñù ‚Üí ‚Ñù} (hf : log_poly_growth f) :
--   poly_growth f :=
-- begin
--   obtain ‚ü®k, hk‚ü© := hf,
--   refine ‚ü®X ^ k, is_O.trans hk _‚ü©,
--   simp only [eval_X, eval_pow],
--   rw is_O_iff,
--   refine ‚ü®1, _‚ü©,
--   rw filter.eventually_at_top,
--   refine ‚ü®1, Œª x hx, _‚ü©,
--   simp [real.norm_eq_abs],
--   by_cases hk : k = 0,
--   { simp [hk] },
--   { sorry } 
-- end

end log_poly_growth